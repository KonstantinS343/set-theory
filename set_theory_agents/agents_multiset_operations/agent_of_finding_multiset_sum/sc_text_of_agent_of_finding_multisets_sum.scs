sc_text_of_agent_of_finding_multisets_sum
-> rrel_key_sc_element:
	agent_of_finding_multisets_sum;

<- scp_program_sc_text;

=> nrel_main_idtf: 
	[sc-текст агентной scp-программы нахождения арифметической суммы мультимножеств] 
	(* <- lang_ru;; *);
	[sc-text scp-agent based program finding arithmetic sum of multisets] 
	(* <- lang_en;; *);;

sc_text_of_agent_of_finding_multisets_sum = [*agent_of_finding_multisets_sum
=> nrel_main_idtf:
	[агентная scp-программа нахождения арифметической суммы мультимножеств] (* <- lang_ru;; *);
	[agent scp-program of finding arithmetic sum of multisets] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_finding_multisets_sum = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_finding_multisets_sum_operator_get_question (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _question;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_check_question_type;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_check_question_type (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finding_multisets_sum;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _question;;

		_=> nrel_else:: .._agent_of_finding_multisets_sum_operator_return;;
		_=> nrel_then:: .._agent_of_finding_multisets_sum_operator_print_start;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_print_start (*
		_<- printNl;;
		_-> rrel_1:: rrel_scp_const:: [Начало. Агент нахождения арифметической суммы мультимножеств];;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_find_multiset_1;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_return (*
		_<- return;;

	*);;

	_-> .._agent_of_finding_multisets_sum_operator_find_multiset_1 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _question;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _multiset_1;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_1;;

		_=> nrel_else:: .._agent_of_finding_multisets_sum_operator_print_end;;
		_=> nrel_then:: .._agent_of_finding_multisets_sum_operator_find_multiset_2;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_find_multiset_2 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _question;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _multiset_2;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_2;;

		_=> nrel_else:: .._agent_of_finding_multisets_sum_operator_print_end;;
		_=> nrel_then:: .._agent_of_finding_multisets_sum_operator_find_sum;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_print_end (*
		_<- printNl;;
		_-> rrel_1:: rrel_scp_const:: [Конец. Агент нахождения арифметической суммы мультимножеств];;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_return;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_find_sum (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_finding_multisets_sum;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_multisets_sum_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _multiset_1;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _multiset_2;;
			_-> rrel_3:: rrel_assign:: rrel_scp_var:: _multiset_3;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _proc_descriptor;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_find_sum_return;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_find_sum_return (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _proc_descriptor;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_sum_to_answer;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_sum_to_answer (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_add_multiset_to_answer;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_multisets_sum_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _multiset_3;;
			_-> rrel_2:: rrel_assign:: rrel_scp_var:: _answer;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _proc_descriptor;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_sum_to_answer_return;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_sum_to_answer_return (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _proc_descriptor;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_generate_tuple;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_generate_tuple (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_scp_var:: _sum_tuple;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _multiset_1;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_multiset_2_to_tuple;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_multiset_2_to_tuple (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _sum_tuple;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _multiset_2;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_tuple_to_answer;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_tuple_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _sum_tuple;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_tuple_arcs_to_answer;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_tuple_arcs_to_answer (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _sum_tuple;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _multiset;;
		_-> rrel_set_2:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_set_3:: rrel_fixed:: rrel_scp_var:: _answer;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_generate_relation;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_generate_relation (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _sum_tuple;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _multiset_3;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_multiset_arithmetic_sum;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_tuple_relation_arcs_to_answer;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_tuple_relation_arcs_to_answer (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _sum_tuple;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _binary_arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _multiset_3;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_multiset_arithmetic_sum;;
		_-> rrel_set_2:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_set_4:: rrel_fixed:: rrel_scp_var:: _answer;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_add_relation_to_answer;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_add_relation_to_answer (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_multiset_arithmetic_sum;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_generate_answer_relation;;
	*);;

	_-> .._agent_of_finding_multisets_sum_operator_generate_answer_relation (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _question;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_of_finding_multisets_sum_operator_print_end;;
	*);;

*);;

*];;
*];;