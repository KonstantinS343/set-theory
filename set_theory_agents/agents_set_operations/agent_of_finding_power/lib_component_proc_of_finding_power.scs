lib_component_proc_of_finding_power
=> nrel_main_idtf:
	[Компонент библиотеки. scp-программа нахождения мощности множества]
	(* <- lang_ru;; *);
	[Library component. scp-program of buildings power]
	(* <- lang_en;; *);

<- platform_independent_reusable_component_OSTIS;
<- reusable_scp_program;

-> rrel_key_sc_element: 
	proc_of_finding_power;;

lib_component_proc_of_finding_power = [*
scp_program -> proc_of_finding_power 
	(*
		-> rrel_params: ...
			(*
    				-> rrel_1: rrel_in: _set;;
	   			-> rrel_2: rrel_in: _i;;
  			*);;
		-> rrel_operators: ...
			(*
    				-> rrel_init: _proc_of_finding_power_operator1 
					(*
						<- searchSetStr3;;
						    
						-> rrel_1: rrel_fixed: rrel_scp_var: _set;;
						-> rrel_2: rrel_scp_var: rrel_assign: rrel_pos_const_perm: _arcsn;;
						-> rrel_3: rrel_scp_var: rrel_assign: _elsn;;

						-> rrel_set_2: rrel_assign: rrel_scp_var: _set_temp;;

						=> nrel_then: _proc_of_finding_power_operator2;;
						=> nrel_else: _proc_of_finding_power_operator_return;;
					*);;
				-> _proc_of_finding_power_operator2 
					(*
						<- searchElStr3;;

						-> rrel_1: rrel_fixed: rrel_scp_var: _set_temp;;
						-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
						-> rrel_3: rrel_assign: rrel_scp_var: _el;;

						=> nrel_then: _proc_of_finding_power_operator3;;
				      	  	=> nrel_else: _proc_of_finding_power_operator5;;
					*);;
				-> _proc_of_finding_power_operator3 
					(*
						<- eraseEl;;

						-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc;;

						=> nrel_goto: _proc_of_finding_power_operator4;;
					*);;
				-> _proc_of_finding_power_operator4 
					(*
				            	<- contAdd;;

				            	-> rrel_1: rrel_fixed: rrel_scp_var: _i;;
						-> rrel_2: rrel_fixed: rrel_scp_var: _i;;
						-> rrel_3: rrel_fixed: rrel_scp_const: [1];;
						 
						=> nrel_goto: _proc_of_finding_power_operator2;;
					*);;
        			-> _proc_of_finding_power_operator5 
					(*
						<- eraseEl;;
						
						-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_temp;;

						=> nrel_goto: _proc_of_finding_power_operator_return;;
					*);;
	    			-> _proc_of_finding_power_operator_return
					(*
	    	  				<- return;;
				    	*);;
			*);;
	*);;
*];;