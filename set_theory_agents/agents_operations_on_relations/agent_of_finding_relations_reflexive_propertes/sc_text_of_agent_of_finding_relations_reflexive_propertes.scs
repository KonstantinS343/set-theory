sc_text_of_agent_of_finding_relations_reflexive_propertes
-> rrel_key_sc_element:
	agent_of_finding_relations_reflexive_propertes;
<- scp_program_sc_text;;

sc_text_of_agent_of_finding_relations_reflexive_propertes = [*
agent_of_finding_relations_reflexive_propertes
=> nrel_main_idtf:
	[агентная scp-программа поиска рефлексивных свойств отношения] 
	(* <- lang_ru;; *);

	[agent scp-program of finding relation's reflexive propertes] 
	(* <- lang_en;; *);
<- agent_scp_program;;

scp_program -> agent_of_finding_relations_reflexive_propertes 
	(*
	-> rrel_params: ..agent_of_finding_relations_reflexive_propertes_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	->rrel_operators: ..agent_of_finding_relations_reflexive_propertes_operator_set 
		(*
		->rrel_init: _agent_of_finding_relations_reflexive_propertes_operator1 
			(*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=>nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator2;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator2 
			(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_finding_relations_reflexive_propertes;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=>nrel_then: _agent_of_finding_relations_reflexive_propertes_operator3;;
			=>nrel_else: _agent_of_finding_relations_reflexive_propertes_operator_return;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator3 
			(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _param;;

			=>nrel_then: _agent_of_finding_relations_reflexive_propertes_operator4A;;
			=>nrel_else: _agent_of_finding_relations_reflexive_propertes_operator_return;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator4A
			(*
			<- searchElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_const: binary_relation;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _param;;

			=> nrel_then: _agent_of_finding_relations_reflexive_propertes_operator5;;	
			=> nrel_else: _agent_of_finding_relations_reflexive_propertes_operator_add_validating_message;;
			*);;
		

		-> _agent_of_finding_relations_reflexive_propertes_operator5 
			(*
			<- call;; 
			-> rrel_1: rrel_fixed: rrel_scp_const:  proc_of_finding_relations_reflexive_propertes ;;
			-> rrel_2:rrel_fixed:rrel_scp_const: 
			_agent_of_finding_relations_reflexive_propertes_operator5_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _param;;
				-> rrel_2: rrel_assign: rrel_scp_var: _answer;;
				*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=>nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator6;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator6 
			(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=>nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator_gen_answer;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator_gen_answer 
			(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

			=>nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator_succesfull;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator_succesfull
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_successfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator_return;;
			*);;  


		-> _agent_of_finding_relations_reflexive_propertes_operator_add_validating_message
			(*
            <- printNl;;
            -> rrel_1: rrel_scp_const: [Wrong parametr! It must be binary relation.];;

		  	=> nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator_gen_unsucces;;
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator_gen_unsucces
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_unsuccessfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: _agent_of_finding_relations_reflexive_propertes_operator_return;; 
			*);;


		-> _agent_of_finding_relations_reflexive_propertes_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;
*];;